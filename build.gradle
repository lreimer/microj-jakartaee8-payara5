plugins {
    id 'maven-publish'
    id 'distribution'
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    jcenter()
}

distributions {
    main {
        contents {
            from("template/") {
            }
            into '/'
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/lreimer/microj-jakartaee8-payara5")
            credentials {
                username = 'lreimer'
                password = project.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN")
            }
        }
    }
    publications {
        template(MavenPublication) {
            pom {
                name = 'Microj Service Template for Jakarta EE 8 on Payara Micro 5'
                description = 'A Microj CLI service template for Jakarta EE 8 with Java 11 running on Payara Micro 5.'
                url = "${project.websiteUrl}"
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                organization {
                    name = 'QAware GmbH'
                    url = 'https://www.qaware.de'
                }
                developers {
                    developer {
                        id = 'lreimer'
                        name = 'Mario-Leander Reimer'
                        email = 'mario-leander.reimer@qaware.de'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/lreimer/microj-jakartaee8-payara5.git'
                    developerConnection = 'scm:git:ssh://github.com/lreimer/microj-jakartaee8-payara5.git'
                    url = 'http://github.com/lreimer/microj-jakartaee8-payara5'
                }
            }

            groupId = "${project.group}"
            artifactId = "${project.name}"
            version = "${project.version}"

            artifact distZip
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : System.getenv('BINTRAY_USERNAME')
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : System.getenv('BINTRAY_API_KEY')
    publications = ['template']
    dryRun = false
    publish = true
    pkg {
        repo = project.bintrayRepo
        name = project.name
        desc = project.description
        licenses = ['MIT']
        labels = ['microservice', 'microj', 'devops']
        websiteUrl = project.websiteUrl
        issueTrackerUrl = project.issueTrackerUrl
        vcsUrl = project.scmUrl
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.description
            released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
            vcsTag = "v${project.version}"
        }
    }
}
